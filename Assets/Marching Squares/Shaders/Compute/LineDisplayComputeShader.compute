#pragma kernel CSMain

#include "SquaresTable.hlsl"

struct SquareLineData
{
    float2 Position[4];
};

RWStructuredBuffer<SquareLineData> LinesData;
RWStructuredBuffer<float> CellWeightData;

int2 CellAmount;
float IsoLevel;
bool HasInterpolation;

int CalculateIndex(int cellX, int cellY)
{
    return cellX + (cellY * CellAmount.x);
}

int CalculateSquareType(float squareWeights[4])
{
    int index = 0;

    if (squareWeights[1] >= IsoLevel)
    {
        index |= 1;
    }

    if (squareWeights[3] >= IsoLevel)
    {
        index |= 2;
    }

    if (squareWeights[2] >= IsoLevel)
    {
        index |= 4;
    }

    if (squareWeights[0] >= IsoLevel)
    {
        index |= 8;
    }
    
    return index;
}

float2 ApplyInterpolation(float2 verticle, int cellX, int cellY)
{
    if (verticle.x == 0.5f)
    {
        int y = (int)verticle.y;
        
        float weightA = CellWeightData[CalculateIndex(cellX, cellY + y)];
        float weightB = CellWeightData[CalculateIndex(cellX + 1, cellY + y)];

        float distance = (IsoLevel - weightA) / (weightB - weightA);
        
        verticle.x = lerp(0, 1, distance);
    }
    else if (verticle.y == 0.5f)
    {
        int x = (int)verticle.x;
        
        float weightA = CellWeightData[CalculateIndex(cellX + x, cellY)];
        float weightB = CellWeightData[CalculateIndex(cellX + x, cellY + 1)];

        float distance = (IsoLevel - weightA) / (weightB - weightA);
        
        verticle.y = lerp(0, 1, distance);
    }
    
    return verticle;
}

[numthreads(8, 1, 1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    int cellX = id.x % CellAmount.x;
    int cellY = id.x / CellAmount.x;
    
    if (cellX >= CellAmount.x - 1 || cellY >= CellAmount.y - 1)
        return;
    
    float squareWeights[4] =
    {
        CellWeightData[CalculateIndex(cellX, cellY)],
        CellWeightData[CalculateIndex(cellX, cellY + 1)],
        CellWeightData[CalculateIndex(cellX + 1, cellY)],
        CellWeightData[CalculateIndex(cellX + 1, cellY + 1)]
    };
    
    int squareIndex = CalculateSquareType(squareWeights);
    
    for (int i = 0; i < 2; i++)
    {
        int indexA = 2 * i;
        int indexB = 2 * i + 1;
        
        int lineA = SquaresLookupTable::LineConnections[squareIndex][indexA];
        int lineB = SquaresLookupTable::LineConnections[squareIndex][indexB];
        
        if (lineB == SquaresLookupTable::InvalidIndex || lineB == SquaresLookupTable::InvalidIndex)
        {
            LinesData[id.x].Position[indexA] = float2(-1, -1);
            LinesData[id.x].Position[indexB] = float2(-1, -1);
        }
        else
        {
            float2 vertA = SquaresLookupTable::SquareVerticles[lineA];
            float2 vertB = SquaresLookupTable::SquareVerticles[lineB];
            
            if (HasInterpolation)
            {
                vertA = ApplyInterpolation(vertA, cellX, cellY);
                vertB = ApplyInterpolation(vertB, cellX, cellY);
            }
            
            LinesData[id.x].Position[indexA] = vertA;
            LinesData[id.x].Position[indexB] = vertB;
        }
    }
}
